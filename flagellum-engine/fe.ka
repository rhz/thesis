# agent signatures
%agent: P(a,b,c,d~0~1)
%agent: Y(p)

# energy costs
%var: '0'  -1
%var: '1'   1
%var: 'Y1' -2
%var: 'Y0'  2
%var: '00' -1
%var: '11' -1
%var: '01'  1
%var: '10'  1

# 10 reversible rules
## 2 binds
'bind 0'   P(d~0,c  ), Y(p  ) -> P(d~0,c!1), Y(p!1) @ [exp] (-1/2 * 'Y0')
'unbind 0' P(d~0,c!1), Y(p!1) -> P(d~0,c  ), Y(p  ) @ [exp] ( 1/2 * 'Y0')

'bind 1'   P(d~1,c  ), Y(p  ) -> P(d~1,c!1), Y(p!1) @ [exp] (-1/2 * 'Y1')
'unbind 1' P(d~1,c!1), Y(p!1) -> P(d~1,c  ), Y(p  ) @ [exp] ( 1/2 * 'Y1')

## 8 flips (conformational change)
### 4 P flips without Y
'flip 000' P(d~0,b!1), P(a!1,d~0,b!2,c), P(a!2,d~0) -> \
           P(d~0,b!1), P(a!1,d~1,b!2,c), P(a!2,d~0) \
           @ [exp] (-1/2 * ('1' - '0' + '01' + '10' - 2 * '00'))
'flip 010' P(d~0,b!1), P(a!1,d~1,b!2,c), P(a!2,d~0) -> \
           P(d~0,b!1), P(a!1,d~0,b!2,c), P(a!2,d~0) \
           @ [exp] ( 1/2 * ('1' - '0' + '01' + '10' - 2 * '00'))

'flip 100' P(d~1,b!1), P(a!1,d~0,b!2,c), P(a!2,d~0) -> \
           P(d~1,b!1), P(a!1,d~1,b!2,c), P(a!2,d~0) \
           @ [exp] (-1/2 * ('1' - '0' + '11' - '00'))
'flip 110' P(d~1,b!1), P(a!1,d~1,b!2,c), P(a!2,d~0) -> \
           P(d~1,b!1), P(a!1,d~0,b!2,c), P(a!2,d~0) \
           @ [exp] ( 1/2 * ('1' - '0' + '11' - '00'))

'flip 001' P(d~0,b!1), P(a!1,d~0,b!2,c), P(a!2,d~1) -> \
           P(d~0,b!1), P(a!1,d~1,b!2,c), P(a!2,d~1) \
           @ [exp] (-1/2 * ('1' - '0' + '11' - '00'))
'flip 011' P(d~0,b!1), P(a!1,d~1,b!2,c), P(a!2,d~1) -> \
           P(d~0,b!1), P(a!1,d~0,b!2,c), P(a!2,d~1) \
           @ [exp] ( 1/2 * ('1' - '0' + '11' - '00'))

'flip 101' P(d~1,b!1), P(a!1,d~0,b!2,c), P(a!2,d~1) -> \
           P(d~1,b!1), P(a!1,d~1,b!2,c), P(a!2,d~1) \
           @ [exp] (-1/2 * ('1' - '0' + 2 * '11' - '10' - '01'))
'flip 111' P(d~1,b!1), P(a!1,d~1,b!2,c), P(a!2,d~1) -> \
           P(d~1,b!1), P(a!1,d~0,b!2,c), P(a!2,d~1) \
           @ [exp] ( 1/2 * ('1' - '0' + 2 * '11' - '10' - '01'))

### 4 P flips with Y
'flip 000 Y' P(d~0,b!1), P(a!1,d~0,b!2,c!_), P(a!2,d~0) -> \
             P(d~0,b!1), P(a!1,d~1,b!2,c!_), P(a!2,d~0) \
             @ [exp] (-1/2 * ('1' - '0' + '01' + '10' - 2 * '00' + 'Y1' - 'Y0'))
'flip 010 Y' P(d~0,b!1), P(a!1,d~1,b!2,c!_), P(a!2,d~0) -> \
             P(d~0,b!1), P(a!1,d~0,b!2,c!_), P(a!2,d~0) \
             @ [exp] ( 1/2 * ('1' - '0' + '01' + '10' - 2 * '00' + 'Y1' - 'Y0'))

'flip 100 Y' P(d~1,b!1), P(a!1,d~0,b!2,c!_), P(a!2,d~0) -> \
             P(d~1,b!1), P(a!1,d~1,b!2,c!_), P(a!2,d~0) \
             @ [exp] (-1/2 * ('1' - '0' + '11' - '00' + 'Y1' - 'Y0'))
'flip 110 Y' P(d~1,b!1), P(a!1,d~1,b!2,c!_), P(a!2,d~0) -> \
             P(d~1,b!1), P(a!1,d~0,b!2,c!_), P(a!2,d~0) \
             @ [exp] ( 1/2 * ('1' - '0' + '11' - '00' + 'Y1' - 'Y0'))

'flip 001 Y' P(d~0,b!1), P(a!1,d~0,b!2,c!_), P(a!2,d~1) -> \
             P(d~0,b!1), P(a!1,d~1,b!2,c!_), P(a!2,d~1) \
             @ [exp] (-1/2 * ('1' - '0' + '11' - '00' + 'Y1' - 'Y0'))
'flip 011 Y' P(d~0,b!1), P(a!1,d~1,b!2,c!_), P(a!2,d~1) -> \
             P(d~0,b!1), P(a!1,d~0,b!2,c!_), P(a!2,d~1) \
             @ [exp] ( 1/2 * ('1' - '0' + '11' - '00' + 'Y1' - 'Y0'))

'flip 101 Y' P(d~1,b!1), P(a!1,d~0,b!2,c!_), P(a!2,d~1) -> \
             P(d~1,b!1), P(a!1,d~1,b!2,c!_), P(a!2,d~1) \
             @ [exp] (-1/2 * ('1' - '0' + 2 * '11' - '10' - '01' + 'Y1' - 'Y0'))
'flip 111 Y' P(d~1,b!1), P(a!1,d~1,b!2,c!_), P(a!2,d~1) -> \
             P(d~1,b!1), P(a!1,d~0,b!2,c!_), P(a!2,d~1) \
             @ [exp] ( 1/2 * ('1' - '0' + 2 * '11' - '10' - '01' + 'Y1' - 'Y0'))

# P ring
%init: 1 (P(a!0 , b!1 ), P(a!1 , b!2 ), P(a!2 , b!3 ), P(a!3 , b!4 ), P(a!4 , b!5 ), \
          P(a!5 , b!6 ), P(a!6 , b!7 ), P(a!7 , b!8 ), P(a!8 , b!9 ), P(a!9 , b!10), \
          P(a!10, b!11), P(a!11, b!12), P(a!12, b!13), P(a!13, b!14), P(a!14, b!15), \
          P(a!15, b!16), P(a!16, b!17), P(a!17, b!18), P(a!18, b!19), P(a!19, b!20), \
          P(a!20, b!21), P(a!21, b!22), P(a!22, b!23), P(a!23, b!24), P(a!24, b!25), \
          P(a!25, b!26), P(a!26, b!27), P(a!27, b!28), P(a!28, b!29), P(a!29, b!30), \
          P(a!30, b!31), P(a!31, b!32), P(a!32, b!33), P(a!33, b!0))

# observables
%obs: 'P01' |P(d~0,b!1), P(a!1,d~1)| # 'P10' = 'P01'
%obs: 'Y'   |Y()|
%obs: 'P0'  |P(d~0)|
%obs: 'P1'  |P(d~1)|
%obs: 'PY'  |Y(p!_)|

# injection and removal of Ys
%var: 'nY' 34
%mod: [T] > 100 do $ADD 'nY' Y()
%mod: [T] > 200 do $DEL 'nY' Y()

# snapshots
%mod: [T] > 50 do $SNAPSHOT "t50"
%mod: [T] > 150 do $SNAPSHOT "t150"
%mod: [T] > 250 do $SNAPSHOT "t250"

# for movies:
# %var: 'E+' 0
# %mod: repeat [E+] = ('E+' + 1) do $SNAPSHOT "fe"; $UPDATE 'E+' [E+] until [false]
